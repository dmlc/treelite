cmake_minimum_required (VERSION 3.16)
project(treelite LANGUAGES CXX C VERSION 0.92)

# check MSVC version
if(MSVC)
  if(MSVC_VERSION LESS 1910)
    message(FATAL_ERROR "Need Visual Studio 2017 or newer to compile Treelite")
  endif()
endif()

option(ENABLE_PROTOBUF "Enable Protobuf" OFF)
option(TEST_COVERAGE "C++ test coverage" OFF)
option(USE_OPENMP "Use OpenMP" ON)
option(DETECT_CONDA_ENV "Enable detection of conda environment for dependencies" ON)
option(BUILD_JVM_RUNTIME "Build Treelite runtime for JVM" OFF)

# When installing dependencies, use Conda environment if available
if(DETECT_CONDA_ENV)
  if(DEFINED ENV{CONDA_PREFIX})
    set(CMAKE_PREFIX_PATH "$ENV{CONDA_PREFIX};${CMAKE_PREFIX_PATH}")
    message(STATUS "Detected Conda environment, CMAKE_PREFIX_PATH set to: ${CMAKE_PREFIX_PATH}")
    if (CMAKE_INSTALL_PREFIX_INITIALIZED_TO_DEFAULT)
      message(STATUS "No CMAKE_INSTALL_PREFIX argument detected, setting to: $ENV{CONDA_PREFIX}")
      set (CMAKE_INSTALL_PREFIX $ENV{CONDA_PREFIX})
    endif (CMAKE_INSTALL_PREFIX_INITIALIZED_TO_DEFAULT)
  else()
    message(STATUS "No Conda environment detected")
  endif()
endif()

include(cmake/ExternalLibs.cmake)
include(cmake/Utils.cmake)
include(cmake/Version.cmake)

add_subdirectory(src)
if(BUILD_JVM_RUNTIME)
  add_subdirectory(runtime/java)
endif()

add_library(treelite SHARED)
add_library(treelite_static STATIC)
add_library(treelite_runtime SHARED)
add_library(treelite_runtime_static STATIC)
target_link_libraries(treelite PRIVATE objtreelite INTERFACE dmlc)
target_link_libraries(treelite_static PRIVATE objtreelite INTERFACE dmlc)
target_link_libraries(treelite_runtime PRIVATE objtreelite_runtime INTERFACE dmlc)
target_link_libraries(treelite_runtime_static PRIVATE objtreelite_runtime INTERFACE dmlc)
set_output_directory(treelite ${PROJECT_BINARY_DIR})
set_output_directory(treelite_static ${PROJECT_BINARY_DIR})
set_output_directory(treelite_runtime ${PROJECT_BINARY_DIR})
set_output_directory(treelite_runtime_static ${PROJECT_BINARY_DIR})

write_version()

set_default_configuration_release()
msvc_use_static_runtime()
